# Theme setup to display git repository information and ruby version information
# Warning: contains emoji, which may or may not work on your system. Mac OS X 10.7+ does support emoji as a natively installed unicode font
# Author Jeremy Olliver <jeremy.olliver@gmail.com>

# Load dependencies.
pmodload 'helper'

function prompt_jeremyolliver_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

# Locates (naively) how/where the current ruby-version was set from
function prompt_jeremyolliver_ruby_source {
  if [ -n "$RBENV_VERSION" ]; then
    echo "shell"
  elif [ -f "$PWD/.ruby-version" ]; then
    echo "local"
  elif [ -f "$PWD/.rbenv-version" ]; then
    echo "local"
  elif [ -f "$HOME/.rbenv/version" ]; then
    echo "global"
  else
    echo "default"
  fi
}

# Show terraform workspace in directories that contain a file matching *.tf
function prompt_jeremyolliver_terraform_workspace {
  if (( $+commands[terraform] )); then
    if [[ -n *.tf(#qN) ]]; then
      echo " %F{magenta}tf%f:%F{magenta}$(terraform workspace show)%f"
    fi
  else
    echo ''
  fi
}

function prompt_jeremyolliver_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_jeremyolliver_precmd

  zstyle ':prezto:module:git:info' verbose 'yes'

  # Display current git-tree. These 3 options are coalesced (e.g. display whichever has output first)
  # branch if possible, position (commit distance from nearest tag), or the commit ref
  zstyle ':prezto:module:git:info:branch' format ':%F{cyan}%b%f'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'

  # behind / ahead
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{white}⬆ %A%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{white}⬇ %B%f%%b'

  # show git stashes
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{yellow}✭%S%f%%b'

  # Show git modified status
  zstyle ':prezto:module:git:info:dirty'     format ' %%B%F{red}±%f%%b'
  zstyle ':prezto:module:git:info:action'    format ':%%B%F{yellow}%s%f%%b'

  # Format of the git-info display
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{cyan}git%f$(coalesce "%b" "%p" "%c")%s%A%B%D%S' \
    'rprompt' '%R/%b - %c'

  # Conditional display depending on if the previous command reported an error status or not
  _error_return_status_display='%(?:%f❯:%F{red}❯)'

  PROMPT='%F{green}%~%f${git_info:+${(e)git_info[prompt]}}$(prompt_jeremyolliver_terraform_workspace) $_error_return_status_display %f'

  if (( $+commands[rbenv] )); then
    RPROMPT='%F{red}ruby: %F{cyan}$(rbenv version-name)%f ($(prompt_jeremyolliver_ruby_source))'
  fi

}

prompt_jeremyolliver_setup "$@"
